plugins {
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'idea'
//apply from: file('gradle/idea.gradle')

sourceCompatibility = 1.6
group = 'me.andrz'
version = '1.0.0'
description = 'Brace expansion from Bash/shell in Java.'

ext.versions = [
        slf4j: "1.7.2",
        logback: "1.0.9",
        junit: "4.10",
        hamcrest: "1.3",
        antlr: "4.0"
]

ext.antlr = [
        grammarpackage: "me.andrz.brace.antlr",
        antlrSource: 'src/main/antlr',
        destinationDir: "src/generated-sources/java"
]

// So CI build doesn't fail, but add to "$GRADLE_USER_HOME/gradle.properties"
ext['bintray.key'] = null

repositories {
    mavenCentral()
}

configurations {
    antlr4 {
        description = "ANTLR4"
    }
}

dependencies {
    compile "org.slf4j:slf4j-api:${versions.slf4j}"
    compile "ch.qos.logback:logback-classic:${versions.logback}"
    compile "org.antlr:antlr4-runtime:${versions.antlr}"
    antlr4 "org.antlr:antlr4:${versions.antlr}"
    testCompile "junit:junit:${versions.junit}"
    testCompile "org.hamcrest:hamcrest-all:${versions.hamcrest}"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
    distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

/**
 * https://github.com/bintray/gradle-bintray-plugin
 */
bintray {
    user = 'AndersDJohnson'
    key = project.property('bintray.key')
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/AndersDJohnson/brace-expansion-java.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = project.version
        }
    }
    publications = ['mavenPublication']
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId 'me.andrz'
            artifactId project.name
            version project.version
            artifacts {
                archives jar
                archives sourceJar
            }
        }
    }
}

idea {
    module {
        sourceDirs += file(antlr.destinationDir)
    }
}

//tasks.withType(Compile) {
//    options.encoding = 'UTF-8'
//}

tasks.withType(Test) {
    systemProperties = System.getProperties()
    testLogging.showStandardStreams = true
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task antlrOutputDir << {
    mkdir(antlr.destinationDir)
}

task generateGrammarSource(dependsOn: antlrOutputDir, type: JavaExec) {
    description = 'Generates Java sources from ANTLR4 grammars.'

    inputs.dir file(antlr.antlrSource)
    outputs.dir file(antlr.destinationDir)

    def grammars = fileTree(antlr.antlrSource).include('**/*.g4')

    main = 'org.antlr.v4.Tool'
    classpath = configurations.antlr4
    def pkg = antlr.grammarpackage.replaceAll("\\.", "/")
    args = ["-o", "${antlr.destinationDir}/${pkg}"/*, "-atn"*/, "-visitor", "-package", antlr.grammarpackage, grammars.files].flatten()

}

compileJava {
    dependsOn generateGrammarSource
    source antlr.destinationDir
}

clean {
    delete antlr.destinationDir
}
